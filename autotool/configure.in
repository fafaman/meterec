
C_PREREQ(2.59)

AC_INIT([meterec], [0.10.0], [fabrice.lebas@gmail.com])

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE([1.9 foreign])

AC_PROG_CC

AC_CHECK_FUNCS(atexit usleep)

AC_CHECK_LIB([mx], [powf], , [
  AC_CHECK_LIB([m], [powf], ,[AC_MSG_ERROR(Can't find libm or libmx supporting powf() installed)])
])

AC_CHECK_LIB([m],       [sqrt],         , [AC_MSG_ERROR(Can't find libm supporting sqrt() installed)])
AC_CHECK_LIB([pthread], [pthread_self], , [AC_MSG_ERROR(Can't find libpthread installed)])


PKG_CHECK_MODULES(NCURSES,   [ncurses >= 5.9]) 
PKG_CHECK_MODULES(JACK,      [jack >= 1.9.8])
PKG_CHECK_MODULES(SNDFILE,   [sndfile >= 1.0.25])
PKG_CHECK_MODULES(LIBCONFIG, [libconfig >= 1.3.2])

PACKAGE_CFLAGS="$SNDFILE_CFLAGS $JACK_CFLAGS $LIBCONFIG_CFLAGS $NCURSES_CFLAGS "
PACKAGE_LIBS="$SNDFILE_LIBS $JACK_LIBS $LIBCONFIG_LIBS $NCURSES_LIBS "

CFLAGS="$CFLAGS $PACKAGE_CFLAGS"
CPPFLAGS="$CFLAGS $PACKAGE_CFLAGS"
LIBS=" $LIBS $PACKAGE_LIBS"

AC_CHECK_HEADERS(jack/session.h)

AC_CHECK_DECL(
 SF_FORMAT_W64,
 [AC_DEFINE(HAVE_W64, [], [Do we have WAV64 file format support in libsndfile])],
 [
  AC_MSG_WARN([Your version of libsndfile does not support the WAV64 format.])
  AC_MSG_WARN([This means that you will be unable to playback or record .w64 files.])
  AC_MSG_WARN([The default output format will be set to WAV.])
  AC_MSG_WARN([Note that WAV format is unable to record files > 2GB.])
 ],
 [#include <sndfile.h>]
)

AC_CHECK_DECL(
 SF_FORMAT_VORBIS,
 [AC_DEFINE(HAVE_VORBIS, [], [Do we have VORBIS file format support in libsndfile])],
 [
  AC_MSG_WARN([Your version of libsndfile does not support the VORBIS format.])
  AC_MSG_WARN([This means that you will be unable to playback or record .ogg .oga .flac files.])
 ],
 [#include <sndfile.h>]
)


AC_CONFIG_FILES([Makefile])
AC_OUTPUT
